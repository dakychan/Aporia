buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:6+'
        classpath "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.0"
    }
}

import net.minecraftforge.gradle.common.util.Utils
import net.minecraftforge.gradle.common.util.VersionJson
import org.jetbrains.gradle.ext.*

apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradle.patcher'
apply plugin: "org.jetbrains.gradle.plugin.idea-ext"
apply plugin: 'kotlin'

println(' Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    shade
    compile.extendsFrom shade
}

group = 'aporia.cc'
version = ''

ext {
    minecraft_version = '1.21.11'
    mcp_version = '20251209.095502'
    mappings_channel = 'official'
    mappings_version = '1.21.11'
    spi_version = '7.0.0'
}

repositories {
    mavenCentral()
    maven {
        name = "ferra13671-maven"
        url = "https://ferra13671.github.io/maven/"
    }
}

dependencies {
    implementation 'net.minecraftforge:forgespi:' + spi_version
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"
    implementation "com.ferra13671:comet-renderer:2.5-HOTFIX-1"
    implementation "com.ferra13671:gl-texture-utils:1.7.4-HOTFIX-1"
    implementation "com.ferra13671:comet-renderer-minecraft:26.1-snapshot-1-fabric-2.5-HOTFIX-1"
    implementation "com.ferra13671:comet-renderer-better-compiler:2.5-HOTFIX-1"
    implementation "com.ferra13671:comet-renderer-better-exceptions:2.5-HOTFIX-1"
    implementation "com.ferra13671:comet-renderer-post-effects:2.5-HOTFIX-1"
    implementation 'ai.catboost:catboost-prediction:1.2.8'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.10"
    implementation "com.ferra13671:discord-ipc:1.3"
    implementation "com.google.code.gson:gson:2.8.9"
}

project(':Aporia') {
    apply plugin: 'net.minecraftforge.gradle.mcp'
    mcp {
        config = minecraft_version + '-' + mcp_version
        pipeline = 'joined'
    }
}

evaluationDependsOn(':Aporia')

patcher {
    parent = project(':Aporia')
    patchedSrc = file('src/main/java')
    mappings channel: mappings_channel, version: mappings_version
    mcVersion = minecraft_version
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

var assetsFolder = file('run/assets')

tasks.register('runclient', JavaExec) {
    group = "MCP"
    description = "Runs the client"
    classpath sourceSets.main.runtimeClasspath
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        jvmArgs '-XstartOnFirstThread'
    }

    var runFolder = file('run')
    var versionJson = Utils.loadJson(downloadAssets.getMeta().get().getAsFile(), VersionJson.class)

    workingDir runFolder
    args '--gameDir', '.'
    args '--version', minecraft_version
    args '--assetsDir', runFolder.relativePath(assetsFolder)
    args '--assetIndex', versionJson.assetIndex.id
    args '--accessToken', '0'
    main 'net.minecraft.client.main.Main'
}


tasks.register('setup') {
    group = "MCP"
    description = "Setups the dev workspace"
    dependsOn ':extractMapped'
    mkdir assetsFolder
    copy {
        from downloadAssets.output.path
        into assetsFolder
    }
}

tasks.register('copyAssets') {
    group = "MCP"
    description = "Download and place the assets into the run folder"
    dependsOn ':downloadAssets'
    mkdir 'run/assets'
    copy {
        from downloadAssets.output.path
        into assetsFolder
    }
}

tasks.register('runserver', JavaExec) {
    mkdir 'runserver'
    group = "MCP"
    description = "Runs the server"
    standardInput = System.in
    classpath sourceSets.main.runtimeClasspath
    main 'net.minecraft.server.Main'
    workingDir 'runserver'
}

idea.project.settings {
    runConfigurations {
        "Minecraft"(Application) {
            mainClass = 'mcp.client.Start'
            workingDirectory = "$projectDir/run"
            moduleName = idea.module.name + '.main'
        }
    }
}
